;  Outputs messages using macros
;
; Copyright (C) 2020 Lawrence Woodman <lwoodman@vlifesystems.com>
; Licensed under a BSD 0-Clause licence. Please see 0BSD_LICENCE.md for details.

.equ        OUT -1
.equ        HALT -1

;========================================
;           Macro Definitions
;========================================
.macro      jump addr
            sble  z z addr
.endm

.macro      inc addr
            sble  minusOne addr
.endm


.macro      outputString stringAddr
.equ        OUT -1
            sble  loop loop
            sble  checkEnd+1 checkEnd+1
            sble  nAddr loop
            sble  nAddr checkEnd+1
loop:       sble  stringAddr OUT     ; Outputs char pointed to by stringAddr
            inc   loop               ; Increments char output ptr to next char
            inc   checkEnd+1         ; Increments end of string ptr to next char
checkEnd:   sble  z stringAddr done  ; Finishes if char at ptr is zero
            jump  loop               ; Jumps to loop
z:          .word 0
nAddr:      .word 0-(stringAddr)
done:
.endm


; Call a function
; Store return address at first addr+2 and begins execution at next address
; This is so that you can just jump to first instruction to return
.macro      call addr
            sble  addr+2 addr+2
            sble  ret z
            sble  z addr+2
jump:       sble  z z addr+3
ret:        .word $+1
.endm


;========================================
;           Start
;========================================
            sble  z z main

;========================================
;           Function Definitions
;========================================
helloWorld: sble  z z 0              ; Return instruction
            outputString  hello
            jump  helloWorld         ; Return


byeWorld:   sble  z z 0              ; Return instruction
            outputString  bye
            jump  byeWorld           ; Return


;========================================
;           Data Storage
;========================================
minusOne:   .word -1                 ; Used to increment ptr
hello:      .ascii "HELLO, WORLD!\n" ; Null-terminated string to print
z:          .word 0
bye:        .ascii "BYE, WORLD!\n"   ; Null-terminated string to print
            .word 0



;========================================
;           Main
;========================================
main:       call  helloWorld
            call  byeWorld
            call  helloWorld
            sble  z z HALT
